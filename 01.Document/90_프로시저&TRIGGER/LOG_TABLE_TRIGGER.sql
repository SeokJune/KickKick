CREATE TABLE AUDIT_DDL
(
   D              DATE,
   OSUSER         VARCHAR2 (255),
   SESSION_USER   VARCHAR2 (255),
   HOST           VARCHAR2 (255),
   TERMINAL       VARCHAR2 (255),
   MODULE         VARCHAR2 (255),
   OWNER          VARCHAR2 (30),
   TYPE           VARCHAR2 (30),
   NAME           VARCHAR2 (30),
   SYSEVENT       VARCHAR2 (30),
   SQL_TEXT        VARCHAR2 (4000)
);

CREATE OR REPLACE TRIGGER AUDIT_DDL_TRG AFTER DDL ON DATABASE
DECLARE
    SQL_TEXT ORA_NAME_LIST_T;
    STMT VARCHAR2(4000):= '';
    N NUMBER;
BEGIN
    N:= ORA_SQL_TXT(SQL_TEXT);
    
    FOR i IN 1..N
    LOOP
        STMT:= SUBSTR(STMT||SQL_TEXT(i), 1, 4000);
    END LOOP;
    
    INSERT INTO AUDIT_DDL VALUES (
        SYSDATE,
        SYS_CONTEXT('USERENV','OS_USER'),
        SYS_CONTEXT('USERENV','SESSION_USER'),
        SYS_CONTEXT('USERENV','HOST'), 
        SYS_CONTEXT('USERENV','TERMINAL'),
        SYS_CONTEXT('USERENV','MODULE'),
        ORA_DICT_OBJ_OWNER,
        ORA_DICT_OBJ_TYPE,
        ORA_DICT_OBJ_NAME,
        ORA_SYSEVENT,
        STMT
    );
END;